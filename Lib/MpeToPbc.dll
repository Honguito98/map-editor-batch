:	MpeToPbc.dll is part of Map Editor.
:	
:	Map Editor is free software: you can redistribute it and/or modify
:	it under the terms of the GNU General Public License as published by
:	the Free Software Foundation, either version 3 of the License, or
:	(at your option) any later version.
:	
:	Map Editor is distributed in the hope that it will be useful,
:	but WITHOUT ANY WARRANTY; without even the implied warranty of
:	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
:	GNU General Public License for more details.
:	
:	You should have received a copy of the GNU General Public License
:	along with Map Editor.  If not, see <http://www.gnu.org/licenses/>.

	:: MpeToPbc Library - Developed By Honguito98 ::
	::! This Cannot Run in mode Standalone !::
	:: Usage: MpeToPbc.dll OUTFILE X1,X2 Y1,Y2::
	:: This requires the: setlocal enabledelayedexpansion enableextensions ::
	:: std sze: X=80,Y=27
	Set LF=^


	Set "Out=%~1"
	Set X2=%3
	Set Y2=%5
	If !Map.X! Lss !X2! Set X2=!Map.X!
	If !Map.Y! Lss !Y2! Set Y2=!Map.Y!
	Set/a "X1=%2*7,X2=(X2-%2)*7,Y1=%4,Y2=Y2" 2>Nul
	Del "!Out!" >Nul 2>&1
	Set/a Count_=0,Int=1
	::::
	Set/a [Lines]=!Y2!-!Y1!,[Count]=0
	If ![Lines]! Geq 1000 If ![Lines]! Leq 9999 Set Bss=!Bs!!Bs!!Bs!!Bs!
	If ![Lines]! Geq 100 If ![Lines]! Leq 999 Set Bss=!Bs!!Bs!!Bs!
	If ![Lines]! Geq 10 If ![Lines]! Leq 99 Set Bss=!Bs!!Bs!
	If ![Lines]! Geq 1 If ![Lines]! Leq 9 Set Bss=!Bs!
	Set Bss=!Bss!!Bs!!Bs!
	Call Lib\Status.bat "%~1" l
	::::
	
	For /L %%# in (!Y1!,1,!Y2!) Do Call :ChgChr_ %%#
	Set/a Int-=1
	For /L %%# in (1,1,!Int!) Do (
		Set ^"Lm%%#=!Lm%%#:^|=^%LF%%LF%!"
		For /F "Tokens=*" %%a in ("!Lm%%#!") Do Echo.%%a>>"!Out!"
		Echo;->>"!Out!"
		Set Lm%%#=
	)
	For %%b in (
	"00"
	"0"
	";"
	":: .::Paint Batch Colous-Program created by Honguito98::."
	) Do Echo.%%~b>>"!Out!"
	For %%a in (Int Tmp_ X Y Count_ Count) Do Set "%%a="
	Exit/b 0

:ChgChr_
	Set/a Count=0,X=1,Count_+=1
	Set "Tmp_=!Line[%1]:~%X1%,%X2%!"
	:::::
	If ![Count]! Equ 1000 Set Bss=!Bss!!Bs!
	If ![Count]! Equ 100  Set Bss=!Bss!!Bs!
	If ![Count]! Equ 10  Set Bss=!Bss!!Bs!
	<Nul Set/p =![Count]!/![Lines]!!Bss!
	Set/a [Count]+=1
	::::
	:ChgChr__
	If "!Tmp_:~%Count%,7!" Equ "" Goto :Eof
	For /F "Tokens=1-2" %%x in ("!Tmp_:~%Count%,7!") Do (
	For /F "Tokens=1 Skip=1" %%a in ('Find /i "%%y" Lib\CharSet.db') Do Set Chr=%%a
		Set Dec=%%x
		Set/a Fg=0x!Dec:~1,1!,Bg=0x!Dec:~0,1!
		For /L %%# in (1,1,!Int!) Do (
			For /F "Tokens=1,2,4 Delims=|" %%a in ("!Lm%%#!") Do (
			If "%%a" Equ "!Fg!" If "%%b" Equ "!Bg!" If "%%c" Equ "!Chr!" (
				Set "Lm%%#=!Lm%%#:;|=;%X%,%Count_%;|!"
				Goto :NoApply
				)
		))

		Set "Lm!Int!=!Fg!|!Bg!|!X!,!Count_!;|!Chr!"
	)
	Set/a Int+=1
	:NoApply
	Set/a Count+=7,X+=1
	::If "!Tmp_:~%Count%,7!" Neq "" Goto :ChgChr__
	Goto :ChgChr__